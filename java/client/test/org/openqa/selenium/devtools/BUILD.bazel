load("//java:test.bzl", "java_test_suite")

DEV_TOOLS_BROWSERS = {
    "chrome": {
        "deps": ["//java/client/src/org/openqa/selenium/chrome"],
        "jvm_flags": ["-Dselenium.browser=chrome"],
    },
    "edge": {
        "deps": ["//java/client/src/org/openqa/selenium/edge"],
        "jvm_flags": ["-Dselenium.browser=edge"],
    },
}

[java_test_suite(
    name = "LargeTests-%s" % name,
    size = "large",
    srcs = glob(["*Test.java"]),
    jvm_flags = values["jvm_flags"],
    deps = values["deps"] + [
        ":test-lib",
        "//java/client/src/org/openqa/selenium/devtools",
        "//java/client/src/org/openqa/selenium/remote",
        "//third_party/java/guava",
        "//third_party/java/junit",
    ],
) for (name, values) in DEV_TOOLS_BROWSERS.items()]

java_library(
    name = "test-lib",
    srcs = glob(
        ["*.java"],
        exclude = [
            "*Tests.java",
            "*Test.java",
        ],
    ),
    deps = [
        "//java/client/src/org/openqa/selenium/devtools",
        "//java/client/src/org/openqa/selenium/remote",
        "//java/client/test/org/openqa/selenium/environment",
        "//java/client/test/org/openqa/selenium/testing:test-base",
        "//java/client/test/org/openqa/selenium/testing/drivers",
        "//third_party/java/assertj",
        "//third_party/java/junit",
    ],
)
